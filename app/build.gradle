apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def gitCommit = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "org.itxtech.daedalus"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 11
        versionName "1.10.0"

        buildConfigField "String", "BUILD_TIME", "\"${System.currentTimeMillis()}\""
        buildConfigField "String", "GIT_COMMIT", "\"${gitCommit}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = outputFile.name.replace("app", "${defaultConfig.applicationId}_${defaultConfig.versionName}_${gitCommit}")
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:percent:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'

    compile 'org.pcap4j:pcap4j-core:1.7.0'
    compile 'org.pcap4j:pcap4j-packetfactory-propertiesbased:1.7.0'
    compile 'de.measite.minidns:minidns-core:0.2.2'
    compile 'com.google.code.gson:gson:2.8.1'

    compile 'com.google.firebase:firebase-crash:11.0.4'

    testCompile 'junit:junit:4.12'
}
